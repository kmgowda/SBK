/**
 * Copyright (c) KMG. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 */

plugins.withId('maven-publish') {

    group = sbkGroup
    setVersion(sbkVersion)

    java {
        withJavadocJar()
        withSourcesJar()
    }

    /*
      Use command : ./gradlew publish  -Pmaven -Pgithub
      -Pmaven - parameter to publish in maven central
      -Pgithub - parameter to publish to Git hub
     */
    publishing {
        repositories {
            if (project.gradle.startParameter.taskNames.contains("publish")) {
                if (!project.hasProperty("maven") && !project.hasProperty("github")) {
                    println "Missing parameters -Pmaven or/and -Pgithub"
                    return
                }
                if (project.hasProperty("maven")) {
                    println "Publishing SBK packages to Maven Central"

                    /*
                    maven {
                        name = "SBK-Maven"
                        url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                        credentials {
                            username = project.findProperty("oss.username") ?: System.getenv("OSS_USERNAME")
                            password = project.findProperty("oss.password") ?: System.getenv("OSS_PASSWORD")
                        }
                    }
                     */

                    jreleaser {
                        signing {
                            active = 'ALWAYS'
                            armored = true
                        }
                        deploy {
                            maven {
                                mavenCentral {
                                    sonatype {
                                        active = 'ALWAYS'
                                        url = 'https://central.sonatype.com/api/v1/publisher'
                                        stagingRepository('target/staging-deploy')
                                    }
                                }
                            }
                        }
                    }



                }
                if (project.hasProperty("github")) {
                    println "Publishing SBK packages to Git Hub"
                    maven {
                        name = "SBK-GitHub"
                        url = uri("https://maven.pkg.github.com/kmgowda/SBK")
                        credentials {
                            username = project.findProperty("github.username") ?: System.getenv("GITHUB_USERNAME")
                            password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
                        }
                    }
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from(components.java)

                pom {
                    groupId = group
                    name = "SBK"
                    url = "https://github.com/kmgowda/sbk"
                    description = "Storage Benchmark Kit"
                    scm {
                        url = 'https://github.com/kmgowda/sbk/tree/master'
                        connection = 'scm:git:git://github.com/kmgowda/sbk.git'
                        developerConnection = 'scm:git:https://github.com/kmgowda/sbk.git'
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'kmgowda'
                            name = 'Keshava Munegowda'
                            email = 'keshava.gowda@gmail.com'
                        }
                    }
                }
            }
        }

        if (!project.property("signing.keyId")) {
            project.setProperty("signing.keyId", System.getenv("SIGNING_KEYID"))
        }

        if (!project.property("signing.password")) {
            project.setProperty("signing.password", System.getenv("SIGNING_PASSWORD"))
        }

        if (!project.property("signing.secretKeyRingFile")) {
            def secretKeyPath = project.file("${System.getProperty("user.home")}/.gnupg/secring.gpg").absolutePath
            project.setProperty("signing.secretKeyRingFile", secretKeyPath)
        }

        if (project.property("signing.keyId") && project.property("signing.password") && project.property("signing.secretKeyRingFile")) {
            signing {
                sign publications.mavenJava
            }
        }
    }
}