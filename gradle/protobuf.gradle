/**
 * Copyright (c) KMG. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 */
plugins.withId('com.google.protobuf') {
    protobuf {
        // gradle protobuf plugin 0.9.2 : https://github.com/google/protobuf-gradle-plugin/releases/tag/v0.9.2
        // onwards the generatedFilesBaseDir is deprecated
       // generatedFilesBaseDir = "$projectDir/src/generated"
        protoc {
            artifact = "com.google.protobuf:protoc:${protobufProtocVersion}"
        }
        plugins {
            grpc_java {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc_java {
                    outputSubDir = "java"
                }
            }
        }
    }

    clean {
        delete protobuf.generatedFilesBaseDir
    }

    sourceSets {
        main.java.srcDirs "${protobuf.generatedFilesBaseDir}/main/java"
    }

    dependencies {
        api group: 'com.google.protobuf', name: 'protobuf-java', version: protobufProtocVersion
        api group: 'io.grpc', name: 'grpc-netty', version: grpcVersion
        api group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
        api group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
    }

    idea {
        module {
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        }
    }

    sourcesJar.dependsOn(['generateProto', 'generateTestProto'])
    ideaModule.dependsOn(['generateProto', 'generateTestProto'])
    eclipseClasspath.dependsOn(['generateProto', 'generateTestProto'])
}